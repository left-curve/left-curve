# Ensure these are consistent with the values in `docker-compose.yml`.
DB_HOST := "db"
DB_PORT := "5432"
DB_USER := "postgres"
DB_NAME := "grug_dev"

# List available recipes
default:
  @just --list

start-dango:
    set -a; source .env; set +a; \
    INDEXER__CLICKHOUSE__URL="http://localhost:8123" \
    INDEXER__DATABASE__URL="postgres://postgres@localhost:5432/grug_dev" \
    TENDERMINT__RPC_ADDR="http://localhost:26657" \
    cargo run --bin dango start --home ./configs/dango

start-dango-release:
  set -a; source .env; set +a; \
  INDEXER__CLICKHOUSE__URL="http://localhost:8123" \
  INDEXER__DATABASE__URL="postgres://postgres@localhost:5432/grug_dev" \
  TENDERMINT__RPC_ADDR="http://localhost:26657" \
  cargo run --release --bin dango start --home ./configs/dango

# Start the localnet
start-inner profiles="" files="":
  #!/usr/bin/env bash
  set -e

  cleanup() {
    local exit_code=$?
    case "$1" in
      "SIGINT")
        echo "Received SIGINT. Shutting down..."
        ;;
      "SIGTERM")
        echo "Received SIGTERM. Shutting down..."
        ;;
      "")
        if [ $exit_code -ne 0 ]; then
          echo "Command failed with exit code $exit_code. Shutting down..."
        fi
        ;;
    esac

    docker compose {{files}} --profile analytics --profile faucet --profile dango --profile dex-bot down --remove-orphans
    exit $exit_code
  }

  trap 'cleanup SIGINT' INT
  trap 'cleanup SIGTERM' TERM
  trap 'cleanup' ERR

  docker compose {{files}} {{profiles}} up --wait
  docker compose {{files}} logs -f

start-without-dango:
      FAUCET_DANGO_ENDPOINT=http://host.docker.internal:8080 \
      DANGO_ABCI_ENDPOINT=tcp://host.docker.internal:26658 \
      just start-inner "--profile analytics --profile faucet" "-f docker-compose.yml"

start-dex-bot:
      DEX_BOT_DANGO_ENDPOINT=http://host.docker.internal:8080 \
      docker compose --profile dex-bot up dex-bot dex-bot-init --wait

start:
  just start-inner "--profile analytics --profile faucet --profile dango --profile dex-bot" "-f docker-compose.yml -f docker-compose.dango.yml"

# Stop the localnet
stop:
  docker compose down --remove-orphans

logs:
  docker compose logs -f db -f dango -f cometbft -f clickhouse -f dex-bot

# Delete the generate data in order to restart a new localnet from scratch
reset:
  rm -rf ./configs/dango/data
  rm -rf ./configs/dango/indexer
  docker compose down -v dango cometbft prometheus grafana alertmanager loki db clickhouse dex-bot faucet promtail push_gateway node-exporter
  # `docker compose down -v` should delete volumes, but leaving just in case
  docker volume rm -f localdango_prometheus_data localdango_grafana_data localdango_alertmanager_data localdango_loki_data localdango_db_data localdango_dango_data localdango_dango_indexer localdango_cometbft_data localdango_clickhouse_data localdango_clickhouse_logs

full-reset:
  docker compose down --remove-orphans
  docker network prune -f
  docker container prune -f

# Run the development database
run-dev-db:
  docker compose up db

# Check whether the development database is running
check-dev-db:
  docker compose run --rm db pg_isready -h {{DB_HOST}} -p {{DB_PORT}} -U {{DB_USER}}

restore-clickhouse filename:
  docker compose exec clickhouse clickhouse-client --query "RESTORE DATABASE testnet_dango_production FROM File('/var/log/clickhouse-server/backups/{{filename}}')"

# Create the development database
create-dev-db:
  docker compose run --rm db createdb -h {{DB_HOST}} -p {{DB_PORT}} -U {{DB_USER}} {{DB_NAME}}

# Remove the development database
drop-dev-db:
  docker compose run --rm db dropdb -h {{DB_HOST}} -p {{DB_PORT}} -U {{DB_USER}} {{DB_NAME}}

# Migrate the development database
migrate-dev-db:
  sea-orm-cli migrate up -d sql-migration

# Reset the development database
refresh-dev-db:
  sea-orm-cli migrate refresh

# Generate entity from the datatabase for Grug
generate-grug-entity:
  sea-orm-cli generate entity -o sql/src/entity --model-extra-derives 'Default' --ignore-tables 'transfers,accounts,dango_seaql_migrations,grug_seaql_migrations,seaql_migrations'

# Generate entity from the datatabase for Dango
generate-dango-entity:
  sea-orm-cli generate entity -o sql/src/entity --model-extra-derives 'Default' --ignore-tables 'blocks,messages,transactions,events,dango_seaql_migrations,grug_seaql_migrations,seaql_migrations'
